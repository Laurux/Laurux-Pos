' Gambas module file

Public LocalSettings As Settings
Public GlobalSettings As Settings
Public version_sha1 As String = "SHA1_REPO_GIT_Laurux"
Public version_branch As String = "BRANCH_REPO_GIT_Laurux"
Public Son As Integer = 0
Public Societe As String
Public Musique As String = "son/warning.ogg"
Public R As Integer = 255
Public G As Integer = 252
Public B As Integer = 216
Public $cai As String
Public $tcai As String
Private Fontes As String


Public Sub Main()

  Dim Pids As String
  Dim psExist As Process
  
  Try Start.version_branch = File.Load("branch.txt")
  Try Start.version_sha1 = File.Load("sha1.txt")
  
  If Exist(User.home & "/" & Application.Name & ".lock") Then
    Pids = File.Load(User.home & "/" & Application.Name & ".lock")
    If Not IsNull(Pids) Then
      psExist = Shell "ps -p " & Pids Wait
      If psExist.Value = 0 Then
        If Message.Warning(("Il semblerait que " & Application.Name & "\nProcess : " & Pids & "\nsoit encore ouvert!\n\nOuvrir deux fois Laurux peut causer des dysfonctionnements (erreur de stock etc...)\net utiliser un programme qui n'a pas été fermé normalement \nsignifie que la base n'est peut-être pas à jour et que vous devriez faire une restauration."), ("Ne pas ouvrir"), ("Ouvrir")) = 1 Then
          Quit
        Endif
        'Try Kill User.home & "/" & Application.Name & ".lock"
        Lancement()
      Else
        'le process est inexistant
        Try Kill User.home & "/" & Application.Name & ".lock"
        Lancement()
      Endif
    Else
      If Message.Warning(("Il semblerait que " & Application.Name & "\nsoit encore ouvert (Fichier de lock existe mais est vide : " & User.home & "/" & Application.Name & ".lock)!\n\nOuvrir deux fois Laurux peut causer des dysfonctionnements(erreur de stock etc...)\net utiliser un programme qui n'a pas été fermé normalement\nsignifie que la base n'est peut-être pas à jour et que vous devriez faire une restauration."), ("Ne pas ouvrir"), ("Ouvrir")) = 1 Then
        Quit
      Endif
      'Try Kill User.home & "/" & Application.Name & ".lock"
      Lancement()
    Endif
  Else
    Lancement()
  Endif
  
End

Public Sub UnLock()

  Dim Pids As String = ""
  
  Try Pids = File.Load(User.home & "/" & Application.Name & ".lock") & " "
  'first with space separators (very few risk due to linux PID assignement for subset match)
  Pids = Replace$(Pids, " " & CStr(Application.Id), "")
  Pids = Replace$(Pids, CStr(Application.Id) & " ", "")
  'second special case without separators if alone
  Pids = Replace$(Pids, CStr(Application.Id), "")
  If IsNull(Trim(Pids)) Then
    Kill User.home & "/" & Application.Name & ".lock"
  Else
    Pids = Trim(Pids)
    File.Save(User.home & "/" & Application.Name & ".lock", Pids)
  Endif
  
End

Public Sub Lock()

  Dim Pids As String = ""
  
  Try Pids = File.Load(User.home & "/" & Application.Name & ".lock") & " "
  File.Save(User.home & "/" & Application.Name & ".lock", Pids & Application.Id)
  
End


Public Sub Lancement()
  
  Lock()

  Start.LocalSettings = New Settings(User.Home &/ ".config/gambas3" &/ Application.Name & ".conf", Application.Name & " local configuration")
  Try Societe = Start.LocalSettings["/Soc"]
  Try Son = Start.LocalSettings["/son"]
  If Son Then
    Music.Load(Start.Musique)
  Endif
  Try Fontes = Start.LocalSettings["/Soc" & Start.Societe & "/Font"]
  If IsNull(fontes) Then
    Application.Font = Font["Serif,10"]
  Else
    Application.Font = Font[Fontes]
  Endif
  If Not IsNull(Start.LocalSettings["/Cai" & "/Ncai"]) Then
    If Start.LocalSettings["/choixtouches"] Then
      $tcai = Left$(Start.LocalSettings["/Cai" & "/Ncai"], 1)
    Else
      $tcai = 0
    Endif
  Endif
  Fmenu.Show

End
